' To continue execution, even after any Error.
On Error Resume Next

' Declare variables
Dim objWMIService, objProcess, colProcess
Dim strProcessKill, str2ndSLALoc, strFolderDelete, message, successOnes
Dim objShell, intReturn
Dim objFSO, objLog, strLogName, strLogPath
Dim arrFolders(), intCount

strProcessKill = "'SLA.exe'"
strLogName = "Uninstall.log"
Set objShell = CreateObject("WScript.Shell")
str2ndSLALoc = objShell.ExpandEnvironmentStrings("%APPDATA%") & "\SLA"

Set objWMIService = GetObject("winmgmts:" _
& "{impersonationLevel=impersonate}!\\" _
& "." & "\root\cimv2")
Set colProcess = objWMIService.ExecQuery("Select * from Win32_Process Where Name = " & strProcessKill )

Set objFSO = CreateObject("Scripting.FileSystemObject")
strLogPath = objFSO.GetParentFolderName(WScript.ScriptFullName) & "\" & strLogName
Set objLog = objFSO.OpenTextFile(strLogPath, 8, True)

objLog.WriteLine "|"
objLog.WriteLine "——— SLA Uninstallation Log —[ Open ]— " & Date & " " & Time

If Not WScript.Arguments.Named.Exists("elevate") Then
    message = "The script is not running as Administrator"
    objLog.WriteLine "-x- " & message
    intReturn = objShell.Popup(message, 0, "SLA Uninstallation", 64)

    Set objShell = CreateObject("Shell.Application")
    objLog.WriteLine "——— Restarting with elevated privileges..."
    objShell.ShellExecute "wscript.exe", """" & WScript.ScriptFullName & """ /elevate", "", "runas", 1

    WScript.Quit
End If

intCount = 0
For Each objProcess in colProcess
    strFolderDelete = objProcess.ExecutablePath

    objLog.WriteLine "——— Found the Target process : " & strProcessKill & " at " & objProcess.ExecutablePath
    intReturn = objProcess.Terminate()

    If intReturn = 0 Then
        objLog.WriteLine "——— Successfully Terminated  : " & strProcessKill
        intCount = intCount + 1
    Else
        objLog.WriteLine "-x- Termination failed for   : " & strProcessKill & ". Error code: " & intReturn
    End If

    ReDim Preserve arrFolders(intCount - 1)
    arrFolders(intCount - 1) = strFolderDelete
Next

If colProcess.Count = 0 Then
    objLog.WriteLine "-x- No processes named " & strProcessKill & " were found."
Else
    objLog.WriteLine "——— Found " & intCount & " process(es) named " & strProcessKill & " and terminated."

    successOnes = 0
    For i = 0 To UBound(arrFolders)
        strFolderDelete = arrFolders(i)
        strFolderDelete = Left(strFolderDelete, InStrRev(strFolderDelete, "\"))

        If Not objFSO.FolderExists(strFolderDelete) Then
            objLog.WriteLine "-x- The Folder containing SLA.exe can't be deleted, as it is inaccessible, or doesn't exist."
        Else
            objLog.WriteLine "——— Attempting to Delete: " & strFolderDelete & "..."
            intReturn = objShell.Run("cmd /c RD /S /Q """ & strFolderDelete & """", 0, True)

            If intReturn = 0 Then
                objLog.WriteLine "——— Successfully Deleted: " & strFolderDelete
                successOnes = successOnes + 1
            Else
                objLog.WriteLine "-x- Error deleting folder " & strFolderDelete & ". Error code: " & intReturn
            End If

            If Right(strFolderDelete, 1) = "\" Then
                strFolderDelete = Left(strFolderDelete, Len(strFolderDelete) - 1)
            End If

            strFolderDelete = Left(strFolderDelete, InStrRev(strFolderDelete, "\"))

            For Each objFolder In objFSO.GetFolder(strFolderDelete).SubFolders
                If Left(objFolder.Name, 7) = "sla.exe" Then
                    strFolderDelete = objFolder.Path

                    objLog.WriteLine "——— Attempting to Delete: " & strFolderDelete & "..."
                    intReturn = objShell.Run("cmd /c RD /S /Q """ & strFolderDelete & """", 0, True)

                    If intReturn = 0 Then
                        objLog.WriteLine "——— Successfully Deleted: " & strFolderDelete
                        successOnes = successOnes + 1
                    Else
                        objLog.WriteLine "-x- Error deleting folder " & strFolderDelete & ". Error code: " & intReturn
                    End If
                End If
            Next
        End If
    Next
    objLog.WriteLine "——— Deleted " & successOnes & " folder(s) containing " & strProcessKill
End If

If objFSO.FolderExists(str2ndSLALoc) Then
    objLog.WriteLine "——— Attempting to Delete: " & str2ndSLALoc & "..."
    intReturn = objShell.Run("cmd /c RD /S /Q """ & str2ndSLALoc & """", 0, True)

    If intReturn = 0 Then
        objLog.WriteLine "——— Successfully Deleted: " & str2ndSLALoc
    Else
        objLog.WriteLine "-x- Error deleting folder " & str2ndSLALoc & ". Error code: " & intReturn
    End If
End If

objLog.WriteLine "——— SLA Uninstallation Log —[ Exit ]— " & Date & " " & Time
objLog.Close

Set objWMIService = Nothing
Set colProcess = Nothing
Set objShell = Nothing
Set objFSO = Nothing
Set objLog = Nothing
